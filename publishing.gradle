// Master Switches
boolean debug_publishing = false // If true, the tasks will run without the files being actually published
boolean maven_publishing = false // If true, configure the publishing in the publishing { } block
boolean curseforge_publishing = false // If true, configure the publishing in the curseforge { } block
boolean modrinth_publishing = false // If true, configure the publishing in the modrinth { } block

String release_type = 'release' // Can be "alpha", "beta" or "release"
String maven_repository_url = '' // URL pointing to your maven repository

if (maven_publishing) {
    assertProperty 'maven_repository_url'
    publishing {
        publications {
            mavenJava(MavenPublication) {
                from components.java // Publish with standard artifacts
                groupId project.root_package // Publish with root package as maven group
                artifactId project.mod_id // Publish artifacts with mod id as the artifact id

                // Custom artifact:
                // If you want to publish a different artifact to the one outputted when building normally
                // Create a different gradle task (Jar task), in extra.gradle
                // Remove the 'from components.java' line above
                // Add this line (change the task name):
                // artifacts task_name
            }
        }
        repositories {
            maven {
                url maven_repository_url
                credentials PasswordCredentials
            }
        }
    }
}

// Documentation here: https://github.com/matthewprenger/CurseGradle/wiki/
if (curseforge_publishing) {
    apply plugin: 'com.matthewprenger.cursegradle'
    assertEnvironmentVariable 'CURSEFORGE_KEY'
    curseforge {
        apiKey System.getenv('CURSEFORGE_KEY')
        project {
            id 'project-id' // Change to project id (can't be slug)
            addGameVersion 'Java 8'
            addGameVersion 'Forge'
            addGameVersion '1.12.2'
            setReleaseType release_type
            mainArtifact tasks.reobfJar, {
                displayName "${project.property('mod_name')} ${project.property('mod_version')}"
                relations {
                    if (project.use_mixins) {
                        requiredDependency 'mixin-booter'
                    }
                }
            }
            options {
                debug debug_publishing
            }
        }
    }
}

// Documentation here: https://github.com/modrinth/minotaur
if (modrinth_publishing) {
    apply plugin: 'com.modrinth.minotaur'
    assertEnvironmentVariable 'MODRINTH_KEY'
    modrinth {
        token = System.getenv('MODRINTH_KEY')
        projectId = "project-slug" // This can be the project id or the slug.
        versionNumber = project.property('mod_version')
        versionType = release_type
        uploadFile = tasks.reobfJar
        gameVersions ['1.12.2']
        loaders ['forge']
        debugMode = debug_publishing
    }
}
